<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton open source conferencing system - http://www.bigbluebutton.org
  
  Copyright (c) 2010 BigBlueButton Inc. and by respective authors (see below).
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 2.1 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
-->

<containers:MDICanvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:containers="flexlib.mdi.containers.*" >
	
	<mx:Script>
		<![CDATA[
			import flexlib.mdi.containers.MDIWindow;
			
			import mx.controls.Alert;
			import mx.utils.ArrayUtil;
			import org.bigbluebutton.common.IBbbModuleWindow;
			import org.bigbluebutton.common.LogUtil;

			public static const TOP_LEFT:String = "TOP_LEFT_WINDOW";
			public static const BOTTOM_LEFT:String = "BOTTOM_LEFT_WINDOW";
			public static const MIDDLE:String = "MIDDLE_WINDOW";
			public static const BOTTOM:String = "BOTTOM_WINDOW";
			public static const TOP_RIGHT:String = "TOP_RIGHT_WINDOW";
			public static const BOTTOM_RIGHT:String = "BOTTOM_RIGHT_WINDOW";
			public static const POPUP:String = "POP_UP_WINDOW";
			public static const UNTOUCHED:String = "UNTOUCHED_WINDOW";
			
			public static const DESKTOP_SHARING_VIEW:String = "POP_UP_DESKSHARE_VIEW";
			public static const DESKTOP_SHARING_PUBLISH:String = "POP_UP_DESKSHARE_PUBLISH";
			
			private var leftWindowWidth:Number;
			private var leftWindowHeight:Number;
			
			private var centerWindowWidth:Number;
			private var centerWindowHeight:Number;
			
			private var rightWindowWidth:Number;
			private var rightWindowHeight:Number;
			
			private function determineWindowSizes():void{
				
			}
			
			public function addWindow(window:IBbbModuleWindow):void{
				windowManager.add(window as MDIWindow);
				positionWindow(window);
			}
			
			public function removeWindow(window:IBbbModuleWindow):void{
				// the flexlib windowManager remove method doesn't test if the given window is on the windows list
				// this test avoid some exceptions when run the app on debugger flash player
				if (ArrayUtil.getItemIndex(window, windowManager.windowList) != -1) {
					windowManager.remove(window as MDIWindow);
				} else {
					LogUtil.debug("Trying to remove the window [ " + window + " ] but it's not a MainCanvas child");
				}
			}
			
			public function removeAllWindows():void{
				this.windowManager.removeAll();
			}
			
			public function resetWindowLayout():void{
				var windows:Array = this.windowManager.getOpenWindowList();
				for (var i:Number=0; i<windows.length; i++){
					var window:IBbbModuleWindow = windows[i] as IBbbModuleWindow;
					positionWindow(window);
				}
			}
			
			private function positionWindow(window:IBbbModuleWindow):void{
				leftWindowHeight = this.height/3;
				leftWindowWidth = 245;
				rightWindowHeight = this.height * 0.6;
				rightWindowWidth = (this.width / 20) * 6;
				centerWindowHeight = this.height - 10;
				centerWindowWidth = this.width / 2;
				
				var position:String = window.getPrefferedPosition();
				var win:MDIWindow = window as MDIWindow;
				if (win.minimized || win.maximized)
					win.restore();
				var x:Number;
				var y:Number;
				switch(position){
					case TOP_LEFT:
						x = 1;
						y = 1;
						win.width = leftWindowWidth;
						win.height = leftWindowHeight;
						break;
					case BOTTOM_LEFT:
						x = 1;
						y = leftWindowHeight + 10;
						win.width = leftWindowWidth;
						win.height = leftWindowHeight;
						break;
					case MIDDLE:
						x = leftWindowWidth + 10;
						y = 1;
						win.width = centerWindowWidth;
						win.height = centerWindowHeight;
						break;
					case BOTTOM:
						x = 1;
						y = this.height - 300;
						win.width = this.width - 5;
						win.height = 300;
						break;
					case TOP_RIGHT:
						x = this.width - rightWindowWidth - 10;
						y = 1;
						win.width = rightWindowWidth;
						win.height = rightWindowHeight;
						break;
					case BOTTOM_RIGHT:
						x = this.width - rightWindowWidth - 10;
						y = rightWindowHeight + 10;;
						win.width = rightWindowWidth;
						win.height = this.height - rightWindowHeight - 10;
						break;					
					case POPUP:
						x = (Math.random() * this.width) - 640;
						y = (Math.random() * this.height) - 520;
						if (x < 0) x = 1;
						if (y < 0) y = 1;
						break;
					case DESKTOP_SHARING_PUBLISH:
						y = (this.height - win.height) / 2;
						x = (this.width - win.width) / 2;
						break;
					case DESKTOP_SHARING_VIEW:
						x = 1;
						y = 1;
						break;
					case UNTOUCHED:
						// don't reposition the window
						x = win.x;
						y = win.y;
				}
				windowManager.absPos(win, x, y);
			}
			
			private function getVideoWindows():Array
			{
				var windows:Array = this.windowManager.getOpenWindowList();
				var videowindows:Array = new Array();
				for (var i:Number=0; i<windows.length; i++) {
					var window:IBbbModuleWindow = windows[i] as IBbbModuleWindow;
					var type:String = getType(window);
					if (type == "PublishWindow" || type == "VideoWindow")
						videowindows.push(window);
				}
				return videowindows;
			}
			
			private function getType(window:IBbbModuleWindow):String {
				return String(window).substr(String(window).lastIndexOf(".") + 1).match(/[a-zA-Z]+/).join();
			}
			
			public function layoutSmallVideos():void
			{
				var windows:Array = getVideoWindows();
				if (windows.length == 0)
					return;
					
				var width:int = 166;
				var height:int = 149;
				
				var rows:int = Math.ceil(this.height/height);
				var winPerRow:int = Math.floor(windows.length/rows);

				// try to fit better the number of windows per row
				winPerRow = Math.floor(this.width / width);
				rows = Math.ceil(windows.length / winPerRow);
				while (Math.ceil(windows.length / (winPerRow - 1)) == rows) {
					winPerRow -= 1;
				}

				var x:int = 0;
				var y:int = this.height;
							
				for (var i:Number=0; i<windows.length; i++) {
					var window:IBbbModuleWindow = windows[i] as IBbbModuleWindow;
					var type:String = getType(window);
					
					(window as MDIWindow).width = width;
					
					var win:MDIWindow = window as MDIWindow;
					if (win.minimized || win.maximized)
						win.restore();
					windowManager.bringToFront(win);
					
					// change the row
					if (i % winPerRow == 0) {
						x = Math.floor((this.width - (width * winPerRow)) / 2);
						y -= height;
					}

					windowManager.absPos(win, x, y);
					x += win.width;
				}				
			}
			
			public function layoutPresenter():void
			{
			}
			
			public function layoutMeeting():void
			{
			}
			
		]]>
	</mx:Script>
	
</containers:MDICanvas>
